apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.github.triplet.play'


android {
    compileSdkVersion 27
    playAccountConfigs {
        defaultAccountConfig {
            serviceAccountEmail = System.getenv('play_email')
            pk12File = file('key.p12')
        }
    }
    defaultConfig {
        applicationId "krtonga.github.io.differentialaltimetryandroid"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 7
        versionName "0.4.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        playAccountConfig = playAccountConfigs.defaultAccountConfig

        buildConfigField "String", "API_ENDPOINT", "\"http://diffalt.org\""
        resValue "string", "file_provider", "krtonga.github.io.differentialaltimetryandroid.fileprovider"
        resValue "string", "content_provider", "krtonga.github.io.differentialaltimetryandroid.contentprovider"
        resValue "string", "account_type", "krtonga.github.io.differentialaltimetryandroid.account"
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    play {
        track = 'alpha'
    }

    def isRunningOnTravis = System.getenv("CI") == "true"
    if (isRunningOnTravis) {
        signingConfigs.release.storeFile = file("../release.keystore")
        signingConfigs.release.storePassword = System.getenv('keystore_password')
        signingConfigs.release.keyAlias = System.getenv('keystore_alias')
        signingConfigs.release.keyPassword = System.getenv('keystore_alias_password')
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.12'
    testImplementation "android.arch.persistence.room:testing:$room_version"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // views
    implementation "com.android.support:support-v4:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'

    // rx java
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9' // explicit because rxandroid updates slowly
    implementation "com.jakewharton.rxrelay2:rxrelay:2.0.0"

    // logging and analytics
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.0@aar') {
        transitive = true
    }

    // arduino
    implementation 'com.github.felHR85:UsbSerial:4.5.2'

    // db
    implementation "android.arch.persistence.room:runtime:$room_version"
    implementation "android.arch.persistence.room:rxjava2:$room_version"
    kapt "android.arch.persistence.room:compiler:$room_version"

    // location
    implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:5.4.1'
    implementation "com.google.android.gms:play-services-location:$play_version"
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'

    // networking
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
}
